{
	parserClass="cappuccino.ide.intellij.plugin.jstypedef.parser.JsTypeDefParser"
	parserUtilClass="cappuccino.ide.intellij.plugin.jstypedef.parser.JsTypeDefParserUtil"
	extends="cappuccino.ide.intellij.plugin.jstypedef.psi.impl.JsTypeDefElementImpl"
	elementTypeHolderClass="cappuccino.ide.intellij.plugin.jstypedef.psi.types.JsTypeDefTypes"
	elementTypePrefix="JS_"
	elementTypeClass="cappuccino.ide.intellij.plugin.jstypedef.psi.types.JsTypeDefType"
	tokenTypeClass="cappuccino.ide.intellij.plugin.jstypedef.psi.types.JsTypeDefTokenType"
	psiClassPrefix="JsTypeDef"
	psiImplClassSuffix="Impl"
	psiPackage="cappuccino.ide.intellij.plugin.jstypedef.psi"
	psiImplPackage="cappuccino.ide.intellij.plugin.jstypedef.psi.impl"
	generateTokenAccessors=true
	psiImplUtilClass="cappuccino.ide.intellij.plugin.jstypedef.psi.utils.JsTypeDefPsiImplUtil"
	consumeTokenMethod("interfaceBody")="consumeTokenFast"
	elementTypeFactory("classElement|function|interfaceElement|keyList|module|moduleName|property|typeMap|variableDeclaration")="cappuccino.ide.intellij.plugin.jstypedef.psi.types.JsTypeDefTypeFactory.factory"

	name("eos") = "semi-colon"
	name("stringLiteral") = "String"
	name("integer") = "Int"
	//Tokens
	tokens = [
		//Line Terminator
		LINE_TERMINATOR =                 	'___LINE_TERM__'//'regexp:[\r\n\u2028\u2029]'
		WS = 								'regexp:\s+'

		FUNCTION_ARROW						'=>'
		EG =								'='
		COLON =								':'
		SEMI_COLON =						';'
		PIPE =								'|'
		OPEN_ARROW =						'<'
		CLOSE_ARROW =						'>'
		NULLABLE =							'?'
		COMMA =								','
		OPEN_BRACE =						'{'
		CLOSE_BRACE =						'}'
		OPEN_BRACKET = 						'['
		CLOSE_BRACKET =						']'
		OPEN_PAREN =						'('
		CLOSE_PAREN =						')'
		DOT =								'.'
		SINGLE_QUOTE =						"'"
		DOUBLE_QUOTE =						'"'

		//KeyWords
		READONLY =							'readonly'
		VAR =								'var'
		ARRAY =								'Array'
		INTERFACE =							'interface'
		EXTENDS =							'extends'
		CONST =								'constructor'
		FUNCTION_KEYWORD =					'function'
		VOID = 								'void'
		NULL_TYPE =							'null'
		MODULE_KEYWORD =					'module'
		MAP =								'Map'
		TYPE_MAP_KEYWORD = 					'typemap'
		KEYOF = 							'keyof'
		KEYS_KEYWORD =						'keys'
		ALIAS = 							'alias'
		INTERNAL =							'internal'
		STATIC_KEYWORD =                    'static'
		CLASS_KEYWORD = 					'class'
		AT_SILENT =                         '@silent'
		AT_QUIET =                          '@quiet'

		BLOCK_COMMENT_START = 				'/*'
		BLOCK_COMMENT_BODY =				'____BLOCK_COMMENT_BODY__'
		BLOCK_COMMENT_END =					'*/'
		BLOCK_COMMENT =             		'regexp:/\*([^*]|\*[^/])*\*/'
		SINGLE_LINE_COMMENT =       		'regexp://[^\r\n\u2028\u2029]*'
		LINE_TERMINATOR =					'LINETERMINATOR'
		SINGLE_QUOTE_STRING	=				"regexp:'([^\\'\r\n]|\\[\\'brfntvuU0])*'"
		DOUBLE_QUOTE_STRING	= 				'regexp:"([^\\"\r\n]|\\[\\"brfntvuU0])*"'
		INTEGER_LITERAL =					'regexp:[0-9]+'
		ESCAPED_ID =						'regexp:`\s*[_a-zA-Z][_a-zA-Z0-9]*\s*`'
		ID = 								'regexp:[_a-zA-Z][_a-zA-Z0-9]*'
	]
}

defFile ::= fileRootElement*

private fileRootElement
	::= !<<eof>> definition
	;

private definition
	::=	module
	|	interfaceElement
	|	classElement
	|	variableDeclaration
	|	functionDeclaration
	|	typeMapElement
	|	type
	|	keyList eos
	;

module
	::= MODULE_KEYWORD namespacedModuleName '{' moduleElements? '}'
	{
		pin = 1
		extends="cappuccino.ide.intellij.plugin.jstypedef.psi.impl.JsTypeDefStubBasedElementImpl<?>"
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefHasNamespace"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefStubBasedElement<cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefModuleStub>"
        ]
        stubClass="cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefModuleStub"
		methods = [
		    getEnclosingNamespaceComponents
	        getNamespacedName
			getNamespaceComponents
			getAllSubModules
			getEnclosingNamespace
			getCollapsedNamespaceComponents
			getNamespaceComponent
		]
	}
	;

private moduleElements
	::= fileRootElement+
	{
		recoverWhile = moduleElements_recover
	}
	;

private moduleElements_recover
	::= !('}')
	;

variableDeclaration
	::= READONLY? VAR /*<<enterMode "variableDeclaration">>*/ property /*<<exitMode "variableDeclaration">>*/ eos
	{
		pin = 2
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefElement"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNoVoid"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefHasNamespace"
		]
		extends="cappuccino.ide.intellij.plugin.jstypedef.psi.impl.JsTypeDefStubBasedElementImpl<?>"
	    methods = [
	        isNullable
	        getPropertyTypes
	        getPropertyNameString
			getEnclosingNamespace
			getEnclosingNamespaceComponents
	        getNamespacedName
	        getNamespaceComponents
	        getNamespaceComponent
	    ]
	  	stubClass="cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefVariableDeclarationStub"
	}
	;

functionDeclaration
	::= FUNCTION_KEYWORD function
	{
		pin = 1
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefHasNamespace"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefElement"
		]
		methods = [
			getEnclosingNamespace
			getEnclosingNamespaceComponents
	        getNamespacedName
	        getNamespaceComponents
	        getNamespaceComponent
	        isStatic
		]
	}
	;

keyList
	::= KEYS_KEYWORD keyName '=' stringLiteral ( '|' stringLiteral)*
	{
		pin = 1
		extends = "cappuccino.ide.intellij.plugin.jstypedef.psi.impl.JsTypeDefStubBasedElementImpl<?>"
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNotInModule"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefStubBasedElement<cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefKeysListStub>"
	  	]
	  	stubClass="cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefKeysListStub"
	};

anonymousFunction
	::= '(' functionPropertiesList? ')' '=>' functionReturnType /*<<exitMode "useMapOf">>*/
	{
		pin = 1

	}
	;

interfaceElement
	::= 'interface' typeName extendsStatement? (interfaceBody | ';')
	{
		pin = 1
		extends="cappuccino.ide.intellij.plugin.jstypedef.psi.impl.JsTypeDefStubBasedElementImpl<?>"
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefClassDeclaration<cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefInterfaceStub>"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefHasNamespace"
		]
		stubClass="cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefInterfaceStub"
		methods = [
			getConstructors
			getClassName
			getEnclosingNamespace
			getEnclosingNamespaceComponents
			getNamespaceComponents
			getNamespaceComponent
			getNamespacedName
			isStatic
		]
	}
	;

classElement
	::= readonlyStatic? 'class' typeName extendsStatement? (interfaceBody | ';')
	{
		pin = 2
		extends="cappuccino.ide.intellij.plugin.jstypedef.psi.impl.JsTypeDefStubBasedElementImpl<?>"
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefClassDeclaration<cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefClassStub>"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefHasNamespace"
		]
        stubClass="cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefClassStub"
		methods = [
			getConstructors
			getClassName
			getEnclosingNamespace
			getEnclosingNamespaceComponents
	        getNamespaceComponents
	        getNamespaceComponent
	        getNamespacedName
	        isStatic
		]
	}
	;

private interfaceBody
	::= '{' interfaceProperties? '}'
	{
		pin = 1
	}
	;

interfaceBodyProperty
	::= interfaceBody
	{
		pin = 1
	}
	;

private interfaceProperties
	::= interfaceProperty+
	{
		recoverWhile = interfaceProperties_recover
	}
	;

private interfaceProperties_recover
	::= !('}')
	;

private interfaceProperty
	::= property eos
	|	interfaceConstructor eos
	|	function eos
	;


extendsStatement
	::= 'extends' typeList
	{
		pin = 1
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNoNull"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNoVoid"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefElement"
		]
	}
	;

private typeList
	::= type ("," type)*
	{
		pin = 2
	}
	;



private types
	::= type ("|" type)*
	{
		pin = 2
	}
	;

type
	::= arrayType
	|	mapType
	| 	typeName
	|  	anonymousFunction
	|	'null'
	{
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefHasNull"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefElement"
		]
	}
	;

keyOfType
	::= genericsKey KEYOF typeMapName
	{
		pin=2
	}
	;

mapType
	::= MAP '<' keyTypes ',' valueTypes '>'
	{
	    pin=1
	}
	;

keyTypes ::= types

valueTypes ::= types

typeMapElement
	::= TYPE_MAP_KEYWORD typeMapName '{' typeMapBody? '}'
	{
		pin = 1
		extends = "cappuccino.ide.intellij.plugin.jstypedef.psi.impl.JsTypeDefStubBasedElementImpl<?>"
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNotInModule"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefStubBasedElement<cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefTypeMapStub>"
	  	]
	  	stubClass="cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefTypeMapStub"
	  	methods = [
	  		getMapName
	  		getKeys
	  		getTypesForKey
	  		getKeyValuePairs
	  	]
	}
	;

private typeMapBody
	::= keyValuePair+
	{
		recoverWhile = typeMapBody_recover
	}
	;

private typeMapBody_recover
	::= !("}")
	;

keyValuePair
	::= stringLiteral ':' types eos
	{
	    pin = 2
		methods = [
			getKey
			getTypesList
			isNullable
		]
	}
	;

genericTypeTypes
	::= '<' types '>'
	{
		pin = 1
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNoNull"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNoVoid"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefElement"
		]
	}
	;

arrayType
	::= ARRAY arrayDimensions? genericTypeTypes
	{
		pin = 1
	}
	;

arrayDimensions
	::= '[' integer ']'
	{
	    pin=1
	}
	;

interfaceConstructor
	::= CONST '(' propertiesList? ')'
	{
	    pin=1
		implements = "cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNotInInterface"
	}
	;

function
	::= completionModifier? 'static'? functionName '(' functionPropertiesList? ')' (':' functionReturnType)?
	{
		pin = 4
		extends="cappuccino.ide.intellij.plugin.jstypedef.psi.impl.JsTypeDefStubBasedElementImpl<?>"
	    methods = [
	        isNullableReturnType
	        getNamespacedName
			getEnclosingNamespace
			getEnclosingNamespaceComponents
	        getNamespaceComponents
	        getNamespaceComponent
	        isStatic
	        getFunctionNameString
	    ]
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefHasNamespace"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefStubBasedElement<cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefFunctionStub>"
	  	]
	  	stubClass="cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefFunctionStub"
	}
	;

functionName
	::= CONST
	|	ESCAPED_ID
	|	ID
	{
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNamedElement"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefElement"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefHasNamespace"
		]
		methods = [
			getName
			setName
	        getNamespacedName
			getEnclosingNamespace
			getEnclosingNamespaceComponents
	        getNamespaceComponents
	        getNamespaceComponent
		]

	}
	;

functionPropertiesList
	::= functionProperty (',' functionProperty)
	{
		pin=2
	}
	;

functionProperty
	::= propertyName ':' (keyOfType|valueOfKeyType|types)
	{
		pin=2
		extends = property
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.JsTypeDefProperty"
		]
	}
	;

propertiesList
	::= property (',' property)*
	{
	    pin=2
		recoverWhile = propertiesList_recover
		implements = [
		    "cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefAllowAnonymousFunctions"
		    "cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefElement"
		]
	}
	;

private propertiesList_recover
	::= !(")")
	;

property
	::= completionModifier? readonlyStatic?  propertyName ':' (types|interfaceBodyProperty)
	{
	    pin = 4
		extends="cappuccino.ide.intellij.plugin.jstypedef.psi.impl.JsTypeDefStubBasedElementImpl<?>"
	    methods = [
	        isNullable
	        getPropertyTypes
	        getPropertyNameString
			getEnclosingNamespace
			getEnclosingNamespaceComponents
	        getNamespacedName
	        getNamespaceComponents
	        getNamespaceComponent
	    ]
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefHasNamespace"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNoKeyMapReturn"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefStubBasedElement<cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefPropertyStub>"
	  	]
	  	stubClass="cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefPropertyStub"
	}
	;

private readonlyStatic
    ::= READONLY STATIC_KEYWORD
    |   STATIC_KEYWORD READONLY
    |   STATIC_KEYWORD
    |   READONLY
    ;

propertyName
	::= ESCAPED_ID
	|	ID
	|	KEYOF
	|	KEYS_KEYWORD
	{
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNamedElement"
		]
		methods = [
			getName
			setName
		]
	}
	;

functionReturnType
	::= valueOfKeyType
	|	types
	|	VOID
	{
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNoKeyOfTypes"
		]
	    methods = [
	        isNullable
	    ]
	}
	;

valueOfKeyType
	::= typeMapName '[' genericsKey ']'
	{
		pin = 3
	}
	;

typeName
	::= ID
	| 	VOID
	|	ARRAY
	|	MAP
	{
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNoVoid"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNamedElement"
		]
		methods = [
			getName
			setName
		]
	}
	;

namespacedModuleName
	::= namespace moduleName
	;

namespace
    ::= (moduleName '.')*
    ;

moduleName
	::= ID
	{
		extends = "cappuccino.ide.intellij.plugin.jstypedef.psi.impl.JsTypeDefStubBasedElementImpl<?>"
		implements = [
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefNamedElement"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefHasNamespace"
			"cappuccino.ide.intellij.plugin.jstypedef.psi.interfaces.JsTypeDefStubBasedElement<cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefModuleNameStub>"
		]
		stubClass = "cappuccino.ide.intellij.plugin.jstypedef.stubs.interfaces.JsTypeDefModuleNameStub"
		methods = [
			getName
			setName
			getNamespaceComponent
			getNamespaceComponents
			getNamespacedName
			getEnclosingNamespaceComponents
			getEnclosingNamespace
			getIndexInDirectNamespace
			getIndexInNamespace
			getFullyNamespacedName
		]
	}
	;

typeMapName
	::= ID
	;

genericsKey
	::= ID
	;

stringLiteral
	::= SINGLE_QUOTE_STRING
	|	DOUBLE_QUOTE_STRING
	{
		methods = [
			getContent
		]
	}
	;

private completionModifier
    ::= AT_SILENT
    |   AT_QUIET
    ;

integer
	::= INTEGER_LITERAL
	;

keyName ::= ID ;

private eos
	::= LINE_TERMINATOR ';'?
	|	<<lineTerminatorAhead>> ';'?
	| 	';'
	;